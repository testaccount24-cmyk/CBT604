       &DUMP   =   1
       &ANCHOR   =   1
       INPUT('INPUT',5,72)

       TEST   =   SPAN('ABCDEFGHIJKLMNOPQRSTUVWXYZ') RPOS(0)
       THREE   =   (LEN(1) LEN(2) . TWO) . GRAM
       TRI   =   TABLE(100,20)
*
READ   OUTPUT   =   INPUT                              :F(DISP)
       TEXT   =   TEXT OUTPUT
NEXT   TEXT   THREE   =   TWO                          :F(READ)
       GRAM   TEST                                     :F(NEXT)
       TRI<GRAM>   =   TRI<GRAM> + 1                   :(NEXT)
*
DISP   OUTPUT   =
       TRI   =   CONVERT(TRI,'ARRAY')                  :F(NONE)
       PROTOTYPE(TRI)   BREAK(',') . N
       OUTPUT   =   'THERE ARE ' N ' TRIGRAMS AS FOLLOWS:'
       OUTPUT   =
       I   =   1
LOOP   OUTPUT   =   TRI<I,1> ' OCCURS ' TRI<I,2> ' TIMES'
+                                                      :F(END)
       I   =   I + 1                                   :(LOOP)
NONE   OUTPUT   =   'THERE ARE NO TRIGRAMS'
END
SCIENTIFIC MEN MUST OFTEN EXPERIENCE A FEELING NOT FAR REMOVED FROM
ALARM, WHEN WE CONTEMPLATE THE FLOOD OF NEW KNOWLEDGE WHICH EACH YEAR
BRINGS WITH IT.  NEW SOCIETIES SPRING INTO EXISTENCE, WITH THEIR
PROCEEDINGS AND TRANSACTIONS, LADEN WITH THE LATEST DISCOVERIES, AND
NEW JOURNALS CONTINUALLY APPEAR IN RESPONSE TO THE GROWING DEMAND FOR
POPULAR SCIENCE.  EVERY YEAR THE ADDITIONS TO THE COMMON STOCK OF
KNOWLEDGE BECOME MORE BULKY, IF NOT MORE VALUABLE; AND ONE IS IMPELLED
TO ASK, WHERE IS THIS TO END?
